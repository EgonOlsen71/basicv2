10 C0=0:C1=1:C2=2:C3=3:C4=4:C5=5:C6=6:C7=7:C8=8:C9=9
20 SC=1024:LL=40
30 DIMA(19,11),B(19,11)
40 A$(1)="north":A$(2)="east":A$(3)="south":A$(4)="west"
50 W$="{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}"
60 SP$="{space*47}"
70 M1=C0:PRINT"{clear}{white}{down}enter dimensions of labyrinth:"
80 INPUT"{down}how wide";H
90 IFH=C0ORH>19THENPRINT"{up*2}";:GOTO80
100 INPUT"{down}how deep";V
110 IFV=C0ORV>10THENPRINT"{up*2}";:GOTO100
120 PRINT"{down}now creating a"H"x"V"labyrinth
130 A=H*V+C1
140 Q=C0:Z=C0:X=INT(H*RND(H))+C1
150 AA=X
160 A(X,0)=C1:C=C2
170 R=X:S=C1:GOTO240
180 IFR<>HGOTO220
190 IFS<>VGOTO210
200 R=C1:S=C1:GOTO230
210 R=C1:S=S+C1:GOTO230
220 R=R+C1
230 IFA(R,S-C1)=C0GOTO180
240 IFR-C1=C0GOTO560
250 IFA(R-C1,S-C1)GOTO560
260 IFS-C1=C0GOTO400
270 IFA(R,S-C2)GOTO400
280 IFR=HGOTO320
290 IFA(R+C1,S-C1)GOTO320
300 X=INT(C3*RND(C1))+C1
310 ONXGOTO870,910,950
320 IFS<>VGOTO350
330 IFZ=C1GOTO380
340 Q=C1:GOTO360
350 IFA(R,S)GOTO380
360 X=INT(C3*RND(C1))+C1
370 ONXGOTO870,910,1020
380 X=INT(C2*RND(C1))+C1
390 GOTO370
400 IFR=HGOTO500
410 IFA(R+C1,S-C1)GOTO500
420 IFS<>VGOTO450
430 IFZ=C1GOTO480
440 Q=C1:GOTO460
450 IFA(R,S)GOTO480
460 X=INT(C3*RND(C1))+C1
470 ONXGOTO870,950,1020
480 X=INT(C2*RND(C1))+C1
490 GOTO470
500 IFS<>VGOTO530
510 IFZ=C1GOTO870
520 Q=C1:GOTO540
530 IFA(R,S)GOTO870
540 X=INT(C2*RND(C1))+C1
550 ONXGOTO870,1020
560 IFS-C1=C0GOTO740
570 IFA(R,S-C2)GOTO740
580 IFR=HGOTO680
590 IFA(R+C1,S-C1)GOTO680
600 IFS<>VGOTO630
610 IFZ=C1GOTO660
620 Q=C1:GOTO640
630 IFA(R,S)GOTO660
640 X=INT(C3*RND(C1))+C1
650 ONXGOTO910,950,1020
660 X=INT(C2*RND(C1))+C1
670 GOTO650
680 IFS<>VGOTO710
690 IFZ=C1GOTO910
700 Q=C1:GOTO720
710 IFA(R,S)GOTO910
720 X=INT(C2*RND(C1))+C1
730 ONXGOTO910,1020
740 IFR=HGOTO820
750 IFA(R+C1,S-C1)GOTO820
760 IFS<>VGOTO790
770 IFZ=C1GOTO950
780 Q=C1:GOTO800
790 IFA(R,S)GOTO950
800 X=INT(C2*RND(C1))+C1
810 ONXGOTO950,1020
820 IFS<>VGOTO850
830 IFZ=C1GOTO180
840 Q=C1:GOTO860
850 IFA(R,S)GOTO180
860 GOTO1020
870 A(R-C1,S-C1)=C
880 C=C+C1:B(R-C1,S-C1)=C2:R=R-C1
890 IFC=AGOTO1120
900 Q=C0:GOTO240
910 A(R,S-C2)=C
920 C=C+C1
930 B(R,S-C2)=C1:S=S-C1:IFC=AGOTO1120
940 Q=C0:GOTO240
950 A(R+C1,S-C1)=C
960 C=C+C1:IFB(R,S-C1)=C0GOTO980
970 B(R,S-C1)=C3:GOTO990
980 B(R,S-C1)=C2
990 R=R+C1
1000 IFC=AGOTO1120
1010 GOTO560
1020 IFQ=C1GOTO1080
1030 A(R,S)=C:C=C+C1:IFB(R,S-C1)=C0GOTO1050
1040 B(R,S-C1)=C3:GOTO1060
1050 B(R,S-C1)=C1
1060 S=S+C1:IFC=AGOTO1120
1070 GOTO240
1080 Z=C1
1090 IFB(R,S-C1)=C0GOTO1110
1100 B(R,S-C1)=C3:Q=C0:GOTO180
1110 B(R,S-C1)=C1:Q=C0:R=C1:S=C1:GOTO230
1120 IFZ<>C1THENX=INT(H*RND(C1))+C1:B(X,V-C1)=B(X,V-C1)+C1
1130 GOSUB1140:POKESC+12*LL+18-H+LL*V+2*AA,30:GOTO1410
1140 REM PRINT MAZE ***
1150 M1=C1:PRINT"{clear}";:IFV=11THEN1170
1160 FORI=C1TO11-V:PRINT:NEXT
1170 FORJ=VTOC1STEP-C1:PRINTSPC(19-H)
1180 FORI=C1TOH:PRINT"{reverse on} ";
1190 IFB(I,J-C1)=C0GOTO1230
1200 IFB(I,J-C1)=C2GOTO1230
1210 PRINT"{black} {white}";
1220 GOTO1240
1230 PRINT" ";
1240 NEXTI
1250 PRINT" ":REM FINISH LINE
1260 PRINTSPC(19-H)"{reverse on} ";
1270 FORI=C1TOH:PRINT"{black} {white}";
1280 IFB(I,J-C1)<C2GOTO1310
1290 PRINT"{black} {white}";
1300 GOTO1320
1310 PRINT" ";
1320 NEXTI:PRINT
1340 NEXTJ
1350 PRINTSPC(19-H)"{reverse on}";:FORI=C1TOH
1360 IFI=AAGOTO1380
1370 PRINT"{space*2}";:GOTO1390
1380 PRINT" {black} {white}";
1390 NEXTI
1400 PRINT" ":RETURN
1410 W=AA
1420 FORJ=C0TOV-C1
1430 FORI=C1TOH
1440 IFJGOTO1470
1450 IFI=WTHENA(I,J)=(C3-B(I,J))*C2:GOTO1490
1460 M=C1:GOTO1480
1470 M=(A(I,J-C1)AND2)/C2
1480 A(I,J)=(C3-B(I,J))*C2+M*C8
1490 IFI=C1THENM=C1:GOTO1510
1500 M=(A(I-C1,J)AND4)/C4
1510 A(I,J)=A(I,J)+M
1520 NEXTI
1530 NEXTJ
1540 PRINT"{clear}";
1550 X=W:Y=C0:Z=C1:EL=C1:ER=C1
1560 GOTO2260
1570 PRINT"{down*2}you can give the following instructions:";
1580 PRINT"{down}{reverse on}f{reverse off}-move forwards one block;"
1590 PRINT"{down}{reverse on}l{reverse off}-turn left{sh space}90 degress;"
1600 PRINT"{down}{reverse on}r{reverse off}-turn right{sh space}90 degrees;"
1610 PRINT"{down}{reverse on}h{reverse off}-help!"
1620 RETURN
1630 GOSUB1140:PRINT"{down}you are here {cm +}, facing "A$(Z)
1640 POKESC+LL*12+18-H+LL*V+2*X-(LL*2)*Y,102
1650 IFY>VGOTO4520
1660 PRINT"{home}{down*24}what now?";
1670 GETZ$:IFZ$=""GOTO1670
1680 IFZ$="l"GOTO1730
1690 IFZ$="r"GOTO1750
1700 IFZ$="f"GOTO2210
1710 IFZ$="h"GOTO1630
1720 GOTO1670
1730 Z=Z-C1:IFZ<C1THENZ=Z+C4
1740 GOTO2260
1750 Z=Z+C1:IFZ>C4THENZ=Z-C4
1760 GOTO2260
1770 RETURN
1780 IFZ=C1THENA1=A-C1:B1=B-C1:GOTO1830
1790 IFZ=C2ANDB<VTHENA1=A:B1=B:GOTO1830
1800 IFZ=C3THENA1=A+C1:B1=B-C1:GOTO1830
1810 IFZ=C4ANDB>C1THENA1=A:B1=B-C2:GOTO1830
1820 EL=C1:RETURN
1830 F=A(A1,B1):IFZ=C1GOTO1850
1840 FORI=C2TOZ:F=(FAND14)/C2+(FANDC1)*C8:NEXT
1850 EL=(FANDC2)/C2:RETURN
1860 IFZ=C1THENA1=A+C1:B1=B-C1:GOTO1910
1870 IFZ=C2ANDB>C1THENA1=A:B1=B-C2:GOTO1910
1880 IFZ=C3THENA1=A-C1:B1=B-C1:GOTO1910
1890 IFZ=C4ANDB<VTHENA1=A:B1=B:GOTO1910
1900 ER=C1:RETURN
1910 F=A(A1,B1):IFZ=C1GOTO1930
1920 FORI=C2TOZ:F=(FAND14)/C2+(FANDC1)*C8:NEXT
1930 ER=(FANDC2)/C2:RETURN
1940 IFB=C0GOTO2020
1950 IFB>VTHENE=C3:RETURN
1960 F=A(A,B-C1):IFZ=C1GOTO1980
1970 FORI=C2TOZ:F=(FAND14)/C2+(FANDC1)*C8:NEXT
1980 C=FANDC1:D=(FAND4)/C4:E=(FAND2)/C2
1990 IFC=C0THENGOSUB1780
2000 IFD=C0THENGOSUB1860
2010 RETURN
2020 C=C0:D=C0:E=-C1
2030 IFZ<>1GOTO2070
2040 E=C1
2050 IFA=WTHENE=C0
2060 RETURN
2070 IFZ=C3THENE=C2:RETURN
2080 IFZ=C2ANDA=HTHENE=C2:RETURN
2090 IFZ=C4ANDA=C1THENE=C2:RETURN
2100 RETURN
2110 IFE>C0GOTO2170
2120 IFZ=C1THENB=B+C1:RETURN
2130 IFZ=C2THENA=A+C1:RETURN
2140 IFZ=C3THENB=B-C1:RETURN
2150 IFZ=C4THENA=A-C1:RETURN
2160 PRINT"{home}{down*10}{right*10}";:RETURN
2170 IFE=C0GOTO2190
2180 PRINT"{ct g}";
2190 S=C1
2200 RETURN
2210 A=X:B=Y
2220 GOSUB1940
2230 GOSUB2110
2240 X=A:Y=B
2250 IFE>C0GOTO1650
2260 A=X:B=Y
2270 GOSUB1940
2280 REM DISPLAY 5 DEPTHS
2290 FORT=C1TO5
2300 GOSUB2410
2310 IFETHENT=C5:GOTO2350
2320 GOSUB2110
2330 GOSUB1940
2340 IFE>C1THENT=C5:GOTO2350
2350 NEXTT
2360 IFX=WANDY=C0ANDZ=C1GOTO2400
2370 IFX<>WANDY=C0ANDZ=C1THENGOSUB2160:PRINT"{up*4}{right*3}external wall":GOTO2390
2380 GOTO2400
2390 GOSUB2160:PRINT"{up*2}{right*2}you're outside!
2400 GOTO1650
2410 ONTGOTO2420,2780,3220,3710,4250
2420 REM DISPLAY DEPTH 1
2430 PRINT"{clear}";
2440 IFE>C2THENRETURN
2450 IFE<C0ORE>C1THENGOSUB2180:RETURN
2460 FORI=C1TO21:IFC=C1THENPRINT"{right*8}{cm m}";:GOTO2520
2470 IFY=C0THENPRINT"{right}{reverse on}"MID$(W$,(IANDC1)+C1,C8);:GOTO2520
2480 IFEL=C1THENPRINT"{right}{cm +*8}";:GOTO2520
2490 IFI=C2THENPRINT"{right}{cm @*7}{cm +}";:GOTO2520
2500 IFI<18THENPRINT"{right*8}{cm +}";:GOTO2520
2510 PRINT"{right}{reverse on}{space*7}{reverse off}{cm +}";
2520 IFE=C0THENPRINTSPC(22);:GOTO2550
2530 IFY=C0THENPRINT"{reverse on}"MID$(W$,(IANDC1)+C1,22);:GOTO2550
2540 PRINT"{cm +*22}";
2550 IFD=C1THENPRINT"{cm g}":GOTO2610
2560 IFY=C0THENPRINT"{reverse on}"MID$(W$,(IANDC1)+C1,C8):GOTO2610
2570 IFER=C1THENPRINT"{cm +*8}":GOTO2610
2580 IFI=C2THENPRINT"{cm +}{cm @*7}":GOTO2610
2590 IFI<18THENPRINT"{cm +}":GOTO2610
2600 PRINT"{cm +}{reverse on}{space*7}"
2610 NEXT
2620 FORI=C1TOC3:IFC=C1OR(X=WANDY=C0)GOTO2680
2630 IFI<>C1THEN2650
2640 IFEL=C0THENPRINT"{right}{reverse on}{cm t*7}N";:GOTO2690
2650 PRINT"{right}{reverse on}"LEFT$(SP$,C8-I)"N"LEFT$(SP$,I-C1);
2660 IFI=C1GOTO2690
2670 GOTO2700
2680 PRINTSPC(C9-I)"{reverse on}{sh pound}"LEFT$(SP$,I-C1);:IFI<>C1THEN2700
2690 IFE=C0THENPRINT"{reverse on}{cm t*22}";:GOTO2710
2700 PRINT"{reverse on}{space*22}";
2710 IFD=C1OR(X=WANDY=C0)GOTO2750
2720 IFI<>C1THEN2740
2730 IFER=C0THENPRINT"{reverse on}M{cm t*7}":GOTO2760
2740 PRINT"{reverse on}"LEFT$(SP$,I-C1)"M"LEFT$(SP$,C8-I):GOTO2760
2750 PRINT"{reverse on}"LEFT$(SP$,I-C1)"{cm asterisk}"
2760 NEXTI
2770 RETURN
2780 REM DISPLAY DEPTH 2
2790 PRINT"{home}";:Z$="{right*9}"
2800 FORI=C1TOC2:PRINTZ$;:IFC=C0GOTO2830
2810 PRINTSPC(I+C1)"M";SPC(C2-I);:GOTO2860
2820 PRINT"{right*3}M";:GOTO2860
2830 IFI=C1THENPRINTSPC(C4):GOTO2860
2840 IFEL=C1THENPRINT"{cm @*4}";:GOTO2860
2850 PRINT"{right*3}{cm @}";
2860 IFE=C0ORI=C1THENPRINTSPC(14);:GOTO2880
2870 PRINT"{cm @*14}";
2880 IFD=C0GOTO2900
2890 PRINTSPC(C2-I)"N":GOTO2930
2900 IFI=C1THENPRINT:GOTO2930
2910 IFER=C1THENPRINT"{cm @*4}":GOTO2930
2920 PRINT"{cm @}"
2930 NEXT
2940 FORI=C1TO15:PRINTZ$;:IFC=C1THENPRINT"{right*3}{cm m}";:GOTO2990
2950 IFEL=C1THENPRINT"{reverse on}{cm +*4}{reverse off}";:GOTO2990
2960 IFI=C3THENPRINT"{cm @*3}{reverse on}{cm +}{reverse off}";:GOTO2990
2970 IFI>12THENPRINT"{reverse on}{space*3}{cm +}{reverse off}";:GOTO2990
2980 PRINT"{space*3}{reverse on}{cm +}{reverse off}";
2990 IFE=C0THENPRINT"{right*14}";:GOTO3010
3000 PRINT"{reverse on}{cm +*14}{reverse off}";
3010 IFD=C1THENPRINT"{cm g}":GOTO3060
3020 IFER=C1THENPRINT"{reverse on}{cm +*4}":GOTO3060
3030 IFI=C3THENPRINT"{reverse on}{cm +}{reverse off}{cm @*3}":GOTO3060
3040 IFI>12THENPRINT"{reverse on}{cm +}{space*3}":GOTO3060
3050 PRINT"{reverse on}{cm +}"
3060 NEXT
3070 FORI=C1TO4:PRINTZ$;:IFC=C1GOTO3120
3080 IFI<>C1GOTO3110
3090 IFEL=C0THENPRINT"{reverse on}{cm t*3}N";:GOTO3130
3100 PRINT"{reverse on}{space*3}N";:GOTO3130
3110 PRINT"{reverse on}"LEFT$(SP$,C4-I)"N"LEFT$(SP$,I-C1);:GOTO3140
3120 PRINTSPC(C4-I)"{reverse on}{sh pound}"LEFT$(SP$,I-C1);:IFI<>C1THEN3140
3130 IFE=C0THENPRINT"{reverse on}{cm t*14}";:GOTO3150
3140 PRINT"{reverse on}{space*14}";
3150 IFD=C1GOTO3190
3160 IFI<>C1THEN3180
3170 IFER=C0THENPRINT"{reverse on}M{cm t*3}":GOTO3200
3180 PRINT"{reverse on}"LEFT$(SP$,I-C1)"M"LEFT$(SP$,C4-I):GOTO3200
3190 PRINT"{reverse on}"LEFT$(SP$,I-C1)"{cm asterisk}":GOTO3200
3200 NEXTI
3210 RETURN
3220 REM DISPLAY DEPTH 3
3230 PRINT"{home}{down*2}";:Z$=Z$+"{right*4}"
3240 FORI=C1TO3:PRINTZ$;:IFC=C0GOTO3260
3250 PRINTSPC(I-C1)"M"SPC(C3-I);:GOTO3290
3260 IFI<C3THENPRINT"{right*3}";:GOTO3290
3270 IFEL=C1THENPRINT"{cm @*3}";:GOTO3290
3280 PRINT"{right*2}{cm @}";
3290 IFE=C0ORI<=C2THENPRINT"{right*8}";:GOTO3310
3300 PRINT"{cm @*8}";
3310 IFD=C0GOTO3330
3320 PRINTSPC(3-I)"N":GOTO3360
3330 IFI<C3THENPRINT:GOTO3360
3340 IFER=C1THENPRINT"{cm @*3}":GOTO3360
3350 PRINT"{cm @}"
3360 NEXT
3370 FORI=C1TO9:PRINTZ$;:IFC=C1THENPRINT"{right*2}{cm m}";:GOTO3420
3380 IFEL=C1THENPRINT"{cm +*3}";:GOTO3420
3390 IFI=C2THENPRINT"{cm @*2}{cm +}";:GOTO3420
3400 IFI>C7THENPRINT"{reverse on}{space*2}{reverse off}{cm +}";:GOTO3420
3410 PRINT"{space*2}{cm +}";
3420 IFE=C0THENPRINT"{right*8}";:GOTO3440
3430 PRINT"{cm +*8}";
3440 IFD=C1THENPRINT"{cm g}":GOTO3490
3450 IFER=C1THENPRINT"{cm +*3}":GOTO3490
3460 IFI=C2THENPRINT"{cm +}{cm @*2}":GOTO3490
3470 IFI>C7THENPRINT"{cm +}{reverse on}{space*2}{reverse off}":GOTO3490
3480 PRINT"{cm +}"
3490 NEXT
3500 FORI=C1TO3:PRINTZ$;:IFC=C1GOTO3560
3510 IFI<>C1THEN3530
3520 IFEL=C0THENPRINT"{reverse on}{cm t*2}N";:GOTO3570
3530 PRINT"{reverse on}"LEFT$(SP$,C3-I)"N"LEFT$(SP$,I-C1);
3540 IFI=C1THEN3570
3550 GOTO3580
3560 PRINTSPC(C3-I)"{reverse on}{sh pound}"LEFT$(SP$,I-C1);:GOTO3540
3570 IFE=C0THENPRINT"{reverse on}{cm t*8}";:GOTO3590
3580 PRINT"{reverse on}{space*8}";
3590 IFD=C1GOTO3650
3600 IFI<>C1THEN3620
3610 IFER=C0THENPRINT"{reverse on}M{cm t*2}":GOTO3690
3620 PRINT"{reverse on}"LEFT$(SP$,I-C1)"{reverse on}M"LEFT$(SP$,C3-I):GOTO3690
3650 PRINT"{reverse on}"LEFT$(SP$,I-C1)"{cm asterisk}"
3690 NEXTI
3700 RETURN
3710 REM DISPLAY DEPTH 4
3720 PRINT"{home}{down*5}";:Z$=Z$+"{right*3}"
3730 FORI=C1TO2:PRINTZ$;:IFC=C0GOTO3770
3740 PRINTSPC(I-C1)"M"SPC(C2-I);:GOTO3810
3770 IFI=C1THENPRINT"{right*2}";:GOTO3810
3790 IFEL=C1THENPRINT"{cm @*2}";:GOTO3810
3800 PRINT"{right}{cm @}";
3810 IFE=C0ORI=C1THENPRINT"{right*4}";:GOTO3830
3820 PRINT"{cm @*4}";
3830 IFD=C0GOTO3870
3840 IFI=C1THENPRINT"{right}";
3850 PRINT"N":GOTO3910
3870 IFI=C1THENPRINT:GOTO3910
3890 IFER=C1THENPRINT"{cm @*2}":GOTO3910
3900 PRINT"{cm @}"
3910 NEXT
3920 FORI=C1TO5:PRINTZ$;:IFC=C1THENPRINT"{right}{cm m}";:GOTO3970
3930 IFEL=C1THENPRINT"{reverse on}{cm +*2}{reverse off}";:GOTO3970
3940 IFI=C1THENPRINT"{cm @}{reverse on}{cm +}{reverse off}";:GOTO3970
3950 IFI>4THENPRINT"{reverse on}{cm t}{cm +}{reverse off}";:GOTO3970
3960 PRINT" {reverse on}{cm +}{reverse off}";
3970 IFE=C0THENPRINT"{right*4}";:GOTO3990
3980 PRINT"{reverse on}{cm +*4}{reverse off}";
3990 IFD=C1THENPRINT"{cm g}":GOTO4040
4000 IFER=C1THENPRINT"{reverse on}{cm +*2}":GOTO4040
4010 IFI=C1THENPRINT"{reverse on}{cm +}{reverse off}{cm @}":GOTO4040
4020 IFI>C4THENPRINT"{reverse on}{cm +} ":GOTO4040
4030 PRINT"{reverse on}{cm +}"
4040 NEXT
4050 FORI=C1TO2:PRINTZ$;:IFC=C1GOTO4100
4060 IFI<>C1THEN4080
4070 IFEL=C0THENPRINT"{reverse on}{cm t}N";:GOTO4130
4080 PRINT"{reverse on}"LEFT$(SP$,C2-I)"N"LEFT$(SP$,I-C1);
4085 IFI=C1THEN4130
4090 GOTO4140
4100 IFI=C1THENPRINT"{right}";
4110 PRINT"{reverse on}{sh pound}";
4120 IFI=C2THENPRINT" ";:GOTO4140
4130 IFE=C0THENPRINT"{reverse on}{cm t*4}";:GOTO4150
4140 PRINT"{reverse on}{space*4}";
4150 IFD=C1GOTO4200
4160 IFI<>C1THEN4180
4170 IFER=C0THENPRINT"{reverse on}M{cm t}":GOTO4230
4180 PRINT"{reverse on}"LEFT$(SP$,I-C1)" "LEFT$(SP$,C2-I):GOTO4230
4200 PRINT"{reverse on}";:IFI=C2THENPRINT" ";
4210 PRINT"{cm asterisk}"
4230 NEXTI
4240 RETURN
4250 REM DISPLAY DEPTH 5
4260 PRINT"{home}{down*7}";:Z$=Z$+"{right*2}"
4270 PRINTZ$;:IFC=C0GOTO4290
4280 PRINT"M";:GOTO4300
4290 PRINT"{cm @}";
4300 IFE=C0THENPRINT"{right*2}";:GOTO4320
4310 PRINT"{cm @*2}";
4320 IFD=C0GOTO4340
4330 PRINT"N":GOTO4350
4340 PRINT"{cm @}"
4350 FORI=C1TO3:PRINTZ$;:IFC=C1GOTO4370
4360 PRINT"{cm +}";:GOTO4380
4370 PRINT"{cm m}";
4380 IFE=C0THENPRINT"{right*2}";:GOTO4400
4390 PRINT"{cm +*2}";
4400 IFD=C0THENPRINT"{cm +}":GOTO4420
4410 PRINT"{cm g}"
4420 NEXT
4430 PRINTZ$;:IFC=C1GOTO4450
4440 PRINT"{reverse on}N";:GOTO4460
4450 PRINT"{reverse on}{sh pound}";
4460 IFE=C1THENPRINT"{reverse on}{space*2}";:GOTO4480
4470 PRINT"{reverse on}{cm t*2}";
4480 IFD=C1GOTO4500
4490 PRINT"{reverse on}M":GOTO4510
4500 PRINT"{reverse on}{cm asterisk}"
4510 RETURN
4520 PRINT"{clear}you're out!
4530 IFM1THENPRINT"{down*4}":GOTO4560
4540 PRINT"{clear}here is the maze":GOSUB1780
4550 GOSUB1140
4560 INPUT"another game";Z$
4570 IFZ$="y"GOTO70
4580 END
4590 FORI=0TO10:FORJ=0TO19
4600 PRINTRIGHT$("0"+MID$(STR$(A(J,I)),2),2)"{right}";:NEXTJ:PRINT:NEXTI
4610 PRINT:PRINT
4620 FORJ=V-1TO0STEP-1:FORI=1TOH
4630 PRINTRIGHT$("0"+MID$(STR$(B(I,J)),2),2)"{right}";:NEXTI:PRINT:NEXTJ
