50 PRINT"{clear}"
60 PRINTCHR$(8)
65 POKE808,PEEK(808)+17
70 POKE36879,25
100 PRINTTAB(4)"{red}flightpath 737"
200 PRINTTAB(10)"{down}by"
300 PRINTTAB(6)"{down}vaughan dow"
302 PRINTTAB(3)"{down*2}{red}copyright(c)1984"
303 PRINT "{right}{down}anirog software ltd."
304 PRINTTAB(2)"{down*3}copying and hiring"
306 PRINTTAB(7)"{down}strictly"
308 PRINTTAB(6)"{down}forbidden!"
310 GOSUB10200
311 FORU=15TO0STEP-.03:POKE36878,U:NEXT:POKE36876,0
800 POKE36879,25:PRINT"{clear}{red}{down}{right*2}flying experience"
810 PRINT"{down*2}{right*2}first solo......1"
820 PRINT"{down}{right*2}stunt pilot.....2"
830 PRINT"{down}{right*2}part-time.......3"
840 PRINT"{down}{right*2}experienced.....4"
850 PRINT"{down}{right*2}professional....5"
860 PRINT"{down}{right*2}test pilot......6"
870 PRINT"{down*2}{reverse on}{right*2}{green}select level 1-6"
875 GETLE$
880 LE=VAL(LE$):IF LE<1ORLE>6THEN875
890 IF LE=1THENMH=5000:LD=3:LS=-20
892 IF LE=2THENMH=6000:LD=3:LS=-15
894 IF LE=3THENMH=7000:LD=2.5:LS=-15
895 IF LE=4THENMH=8000:LD=2.5:LS=-12
896 IF LE=5THENMH=9000:LD=1.8:LS=-10
898 IF LE=6THENMH=9200:LD=1.5:LS=-8
900 PRINT"{clear}":POKE36879,8
905 PRINT"{white}you must cross a range"
910 PRINT"{up}of moutains"MH"feet high"
915 PRINT"{down}landing runway length"LD"miles"
920 IFLE<3THENPRINT"{down}your landings may be"
925 IFLE<3THENPRINT"a little clumsy"
930 IFLE>2THENPRINT"{down}landings must be very gentle"
935 IFLE>1THENPRINT"{down}you may have an enginefire at any stage !"
940 IFLE>3THENPRINT"{down}there are crosswinds{space*2}on approach to land"
950 PRINT"{down}{reverse on}{red}press space bar to fly"
960 GETA$:IFA$<>" "THEN960::IFA$=" "THEN1000
1000 PRINT"{clear}":POKE36879,8
1050 FOR R=37888TO37909:POKER,3:NEXT:FORR=37910 TO38041:POKER,5:NEXT
1060 FORR=37888+154TO38399:POKER,1:NEXT
1100 FOR R=4096TO4096+(153):POKER,160:NEXT
1150 POKE38179,2:POKE38180,2:POKE38245,2:POKE38246,2
1200 PRINT"{white}{reverse on}{down*6}{right}alt{right*5}hdg{right*6}asi"
1300 PRINT"{white}U{sh asterisk*4}IU{sh asterisk}{right*5}{sh asterisk}I{right}U{sh asterisk*4}I"
1400 PRINT"{up}{sh -}{space*4}{sh -}{sh -}:::{sh -}:::{sh -}{right}{sh -}{space*4}{sh -}"
1500 PRINT"{up}J{sh asterisk*4}K{right*10}J{sh asterisk*4}K"
1600 PRINT"{cyan}{reverse on}{down}{right}u/c{right*14}flp"
1700 PRINT"{white}U{sh asterisk*3}I{right*12}U{sh asterisk*3}I"
1800 PRINT"{up}{sh -}{space*3}{sh -}{right*12}{sh -}{space*3}{sh -}"
1900 PRINT"{up}J{sh asterisk*3}K{right*12}J{sh asterisk*3}K"
2000 PRINT"{down}{cyan}{reverse on}rh{right}dist{right*7}fuel{right}tm "
2100 PRINT"{up}{white}{reverse on}{right*2} {right*3}{reverse off}{right}{cm j}{right*2} {right*2}{cm l}{right*4}{reverse on} "
2200 FORT=4558TO4564:POKET,69:NEXT::FORT=4572TO4579:POKET,69:NEXT
2210 POKE4457,62:POKE4458,90:POKE4459,60:POKE4456,66:POKE4460,66
2211 POKE4326,30
2220 POKE4434,66:POKE4435,67:POKE4437,67:POKE4438,66
2230 POKE4412,66:POKE4413,67:POKE4415,67:POKE4416,66
2240 POKE4390,66:POKE4391,67:POKE4393,67:POKE4394,66
2250 POKE4368,66:POKE4369,67:POKE4371,67:POKE4372,66
2260 POKE4346,66:POKE4347,67:POKE4349,67:POKE4350,66
2280 POKE4478,66:POKE4479,67:POKE4481,67:POKE4482,66
2290 POKE4500,66:POKE4501,67:POKE4503,67:POKE4504,66
2300 POKE4522,66:POKE4523,67:POKE4525,67:POKE4526,66
2310 POKE4544,66:POKE4545,67:POKE4547,67:POKE4548,66
2320 POKE4566,66:POKE4567,67:POKE4569,67:POKE4570,66
2330 POKE4351,146:POKE4352,131:POKE4353,73:POKE4373,43:POKE4395,43
2340 POKE4417,43:POKE4439,87:POKE4461,45:POKE4483,45:POKE4505,45
2350 FORR=4375TO4507STEP22:POKER,93:NEXT
2360 POKE4440,31:POKE4365,135:POKE4366,151:POKE4431,134:POKE4432,151
2370 POKE38236,5:POKE38237,5:POKE38238,5:POKE4444,69:POKE4445,69:POKE4446,69
2380 DIMJS(2,2):POKE37139,0:DD=37154:PA=37137:PB=37152
2385 FORI=0TO2:FORJ=0TO2:READJS(J,I):NEXTJ,I
2390 DATA7,0,1,6,8,2,5,4,3
2970 GOSUB11100
2980 GOSUB11000
2990 TI$="000000"
3000 HI=0:CH=45:V=0:RT=5000:UC=1:FL=0:REV=0:FS=0:G=4458:POKE36878,0
3100 PRINT"{home}{down*9}";HI
3105 IFHI<=0THENPOKE4295,48:POKE4296,48
3110 PRINT"{home}{down*9}{right*17}";INT(V)
3150 PRINT"{home}{down*8}{right*8}"INT(CH)
3200 PRINT"{home}{down*20}{right*3}"INT(RA)+1
3210 PRINT"{home}{down*20}{right*13}";INT(RT)
3250 POKE4555,ASC(MID$(TI$,4,1)):POKE4556,ASC(MID$(TI$,5,1)):POKE4557,ASC(RIGHT$(TI$,1))
3260 IFHI<MHORRA<25THENPOKE4387,32:POKE4388,32
3300 GETA$:IFA$=""THENGOTO3402
3310 IFA$="{f1}"THENV=V+20
3320 IFA$="{f3}"THENV=V+10
3330 IFA$="{f5}"THENV=V-10
3340 IFA$="{f7}"THENV=V-20
3350 IFA$="a"THENUC=0:POKE4427,87:POKE4428,87:POKE4429,87:V=V+5
3360 IFA$="z"THENUC=1:V=V-5:GOSUB11100
3370 IFA$="f"THENFL=0:POKE4444,69:POKE4445,69:POKE4446,69
3380 IFA$="v"THENFL=1:POKE4444,102:POKE4445,102:POKE4446,102
3390 IFA$="r"THENREV=1
3400 IFA$="e"ANDHI>0THENFE=1:FS=0
3402 IFV<19THENC=0:GOTO3500
3405 GOSUB13000:J=JS(XX+1,YY+1)
3410 IFJ=4THEND=D-22:X=X+1
3420 IFJ=0THEND=D+22:X=X-1
3430 IFJ=6THENC=C-1:B=B-1
3440 IFJ=2THENC=C+1:B=B+1
3442 IFJ=7THEND=D+22:X=X-1:B=B-1:C=C-1
3444 IFJ=1THEND=D+22:X=X-1:B=B+1:C=C+1
3446 IFJ=5THEND=D-22:X=X+1:B=B-1:C=C-1
3448 IFJ=3THEND=D-22:X=X+1:B=B+1:C=C+1
3450 IFX>5THENX=5
3451 IFV<160THEND=0
3452 IFX<-5THENX=-5
3454 IFD<-110THEND=-110
3456 IFD>110THEND=110
3458 POKEG+D,90
3460 POKEG+D+22,32
3462 IFD<-200THENGOTO3468
3464 POKEG+D-22,32
3466 IFV<150THENX=0:D=0
3468 IFC>3THENC=3
3470 IFC<-3THENC=-3
3474 IFB<-3THENB=-3
3476 IFB>3THENB=3
3478 POKEG-(132)+B,30:POKEG-(132)+B-1,32:POKEG-132+B+1,32
3500 IFREV=0THENVE=INT(V/30)+3
3510 IFVE>14THENVE=15
3520 IFVE<1THENVE=0
3530 IFREV=0THENPOKE36878,VE
3540 HH=INT(V/5)
3550 IFHH>120THENHH=127
3560 IFHH<10THENHH=0
3570 IFREV=0THENPOKE36877,128+HH
3575 IFV>=20THENPOKE36874,200:POKE36875,200:POKE36876,200
4000 IFV>160THENHI=HI+INT((V*X)/10)
4010 IFSGN(CH)=-1THENCH=360+CH
4015 IFCH>359THENCH=CH-360:POKE4282,32:POKE4283,32
4016 IFCH<100THENPOKE4283,32
4018 IFCH<10THENPOKE4282,32
4020 IFV>40THENRT=INT(RT-(V/10))
4030 RA=(RT-300)/100
4040 V=V-X:CH=CH+C
4050 IFHI<1000THENPOKE4298,32
4052 IFHI<100THENPOKE4297,32
4054 IFHI<10THENPOKE4296,32
4055 IFHI<=0THENPOKE4295,48:POKE4296,48
4056 IFHI>9999THENHI=9999
4058 IFV<100THENPOKE4314,32
4060 IFV<10THENPOKE4313,32
4062 IFRA<9THENPOKE4541,32
4064 IFRT<1000THENPOKE4553,32
4066 IFRT<100THENPOKE4552,32
4100 DHI=INT((V*X)/10)
4105 IFDHI>0ANDDHI<100THENPOKE4418,31:POKE4440,32:POKE4396,32
4107 IFDHI=0THENPOKE4440,31:POKE4462,32:POKE4418,32
4110 IFDHI>=100ANDDHI<200THENPOKE4396,31:POKE4418,32:POKE4374,32
4115 IFDHI>=200THENPOKE4374,31:POKE4396,32
4220 IFDHI<0ANDDHI>=-100THENPOKE4462,31:POKE4440,32:POKE4484,32
4225 IFDHI<-100ANDDHI>=-200THENPOKE4484,31:POKE4506,32:POKE4462,32
4230 IFDHI<-200THENPOKE4506,31:POKE4484,32
4300 IFUC=0ANDHI<300THENPRINT"{clear}{down*4}{right*2}undercarriage below{space*3}300ft !":GOTO12000
4310 IFV>600THENPRINT"{clear}{down*5}{right*3}overspeed !":GOTO12000
4320 IFFL=0ANDHI>10ANDHI<300THENPRINT"{clear}{down*5}{right*4}flaps not down !":GOTO12000
4330 IFFL=1ANDV>200THENPRINT"{clear}{down*4}exceeded flap speed !":GOTO12000
4340 IFFL=1ANDV<160ANDHI>0THENPRINT"{clear}{down*5}below flap stall speed":GOTO12000
4350 IFFL=0ANDV<180ANDHI>10THENPRINT"{clear}{down*3}{right*4}stalled-no flaps !":GOTO12000
4360 IFV>100ANDHI<=0ANDINT(CH)<>RHTHENPRINT"{clear}{down*5}{right*4}off runway";RH:GOTO12000
4370 IFSGN(V)=-1THENV=0
4380 IFHI<LSTHENPRINT"{clear}{down*5}{right*2}landed too heavily!":GOTO12000
4390 IFRA<=-LDANDV>40ANDHI<=0THENPRINT"{clear}{down*4}off end of runway !":GOTO12000
4400 IFHI<0ANDRA>0ANDRT<4000THENPRINT"{clear}{down*4}landed short of runway!":GOTO12000
4410 IFRA<=45ANDRA>42ANDHI=0THENPRINT"{clear}{down*5}acceleration too slow!":GOTO12000
4420 IFRA<27ANDRA>25ANDHI<MHTHENPRINT"{clear}{down*5}{right*2}hit the mountains!":GOTO12000
4430 IFRA<0ANDV<20ANDHI<=0ANDINT(CH)=RHTHENPRINT"{home}{down*2}{right*4}a good landing":GOTO14000
4450 IFRA<=30ANDRA>29.5THENGOSUB11000
4460 IFUC=1ANDV>250THENPRINT"{clear}{down*4}exceeded undercarriage speed !":GOTO12000
4500 IFHI>MHANDRA>25THEN POKE4387,160:POKE4388,160
4510 IFHI>3000ANDRA<35ANDRA>25THENPOKE4387,160:POKE4388,160
4600 IFLE=1THENGOTO4660
4620 FW=INT(RND(1)*(35)+5)
4630 IFINT(RA)=FWTHENFS=1
4640 IFFS=1ANDFE=0THENPOKE4453,160:POKE4454,160:V=V-10
4660 IFFE=1THENPOKE4453,32:POKE4454,32
4670 IFFS=1ANDFE=0THENPOKE36878,0
4700 IFREV=1THENPOKE36878,15:POKE36877,230:V=V-15
4710 IFREV=1ANDHI>0THENPRINT"{clear}{down*4}reverse in flight !":GOTO12000
4720 IFREV=1ANDRA>45THENPRINT"{clear}{down*5}reverse on take-off?":GOTO12000
4730 IFRT<0ANDHI>0THENPRINT"{clear}{down*4}{right*4}out of fuel !":GOTO12000
4800 IFLE=4ANDRA<10ANDRA>5THENCH=CH+.1
4810 IFLE=5ANDRA<10ANDRA>4THENCH=CH-.1
4820 IFLE=6ANDRA<10ANDRA>3THENCH=CH+.1
4900 IFRA<=10ANDHI/100>RA-.5ANDHI/100<RA+.5THENPOKE38047,3:POKE4255,160
4910 IFRA<=10ANDHI/100>RA+.5THENPOKE38047,1:POKE4255,160
4920 IFRA<=10ANDHI/100<RA-.5THENPOKE38047,2:POKE4255,160
4950 LR=INT(CH)-RH
4952 IFLR>3THENLR=3
4954 IFLR<-2THENLR=-2
5000 IFHI<50ANDRA>42THENGOSUB10500
5010 IFV>20ANDRA>42ANDHI<50THENGOSUB10600
5020 IFRA>42ANDHI>=50ANDHI<=100THENPRINT"{home}{down}{reverse on}{cyan}{space*22}{white}"
5030 IFRA>42ANDHI>50ANDHI<=200THENPRINT"{home}{down*2}{reverse on}{cyan}{space*22}{white}"
5040 IFRA>42ANDHI>100ANDHI<=300THENPRINT"{home}{down*3}{reverse on}{cyan}{space*22}{white}"
5050 IFRA>42ANDHI>150ANDHI<=400THENPRINT"{home}{down*4}{reverse on}{cyan}{space*22}{white}"
5060 IFRA>42ANDHI>200ANDHI<=500THENPRINT"{home}{down*5}{reverse on}{cyan}{space*22}{white}"
5070 IFRA>42ANDHI>250ANDHI<=600THENPRINT"{home}{down*6}{reverse on}{cyan}{space*22}{white}"
6000 IFSGN(X)=-1ANDRA<23ANDRA>10THENFORR=38041TO37910STEP-1:POKER,5:NEXT
6010 IFSGN(X)=-1ANDRA<22ANDRA>=17ANDHI<MHTHEN GOSUB10650
6012 IFSGN(X)=-1ANDRA<17ANDRA>=12ANDHI<MHTHEN GOSUB10660
6015 IFSGN(X)=-1ANDRA<12ANDRA>5ANDHI<MHTHEN GOSUB10700
6020 IFRA<=5ANDHI<1500THENGOSUB10800
6030 IFRA<=1ANDHI<1500THENGOSUB10900
6990 IFHI<300ANDRA>42ORRA<25THENGOTO7500
7000 PRINT"{home}{cyan}{reverse on}{down}NMNMNMNMNMNMNMNMNMNMNMNM{space*2}M{space*3}M MN N{space*2}M M NM{space*3}NMMN{space*2}MN NNMNM{space*2}MNM{space*2}{white}"
7500 IFRA<25ANDRA>24THENGOSUB10750
10000 GOTO3100
10200 POKE36878,15:FORT=128TO200STEP.1:POKE36876,T:NEXT:FORY=0TO1000:NEXT:RETURN
10300 POKE36875,0:POKE36874,0:POKE36876,0
10302 POKE36878,15:FORT=255TO128STEP-.5:POKE36877,T:NEXT:RETURN
10500 PRINT"{home}"
10505 PRINTTAB(6-LR)"{reverse on}{green}{space*3}N.M{space*18}N{space*3}M{space*16}N{space*2}.{space*2}M{space*3}"
10510 PRINTTAB(3-LR)"{reverse on}{space*3}N{space*7}M{space*12}N{space*4}.{space*4}M{space*10}N{space*11}M{space*3}{white}"
10515 RETURN
10600 PRINT"{home}"
10605 PRINTTAB(6-LR)"{reverse on}{green}{space*3}N M{space*18}N . M{space*16}N{space*5}M{space*3}"
10610 PRINTTAB(3-LR)"{reverse on}{space*3}N{space*3}.{space*3}M{space*12}N{space*9}M{space*10}N{space*5}.{space*5}M{space*3}{white}"
10615 RETURN
10650 PRINT"{home}"
10652 PRINTTAB(6-(LR))"{down*2}{reverse on}{green}{space*3}N.M{space*30}{white}"
10654 RETURN
10660 PRINT"{home}"
10662 PRINTTAB(6-(LR))"{down*2}{reverse on}{green}{space*3}N M{space*18}N...M{space*28}{white}"
10664 RETURN
10700 PRINT"{home}"
10705 PRINTTAB(6-LR)"{down*2}{reverse on}{green}{space*3}N{cm t}M{space*18}N{space*3}M{space*16}N.....M{space*8}{white}"
10710 RETURN
10750 PRINT"{home}{down}{cyan}{reverse on}{space*70}{white}"
10770 RETURN
10800 PRINT"{home}"
10805 PRINTTAB(6-LR)"{reverse on}{green}{space*3}N.M{space*18}N{space*3}M{space*16}N{space*2}.{space*2}M{space*3}"
10810 PRINTTAB(3-LR)"{up}{reverse on}{space*3}N{space*7}M{space*12}N{space*4}.{space*4}M{space*10}N{space*11}M{space*3}{white}"
10815 RETURN
10900 PRINT"{home}"
10905 PRINTTAB(6-LR)"{reverse on}{green}{space*3}N M{space*18}N . M{space*16}N{space*5}M{space*3}"
10910 PRINTTAB(3-LR)"{up}{reverse on}{space*3}N{space*3}.{space*3}M{space*12}N{space*9}M{space*10}N{space*5}.{space*5}M{space*3}{white}"
10915 RETURN
11000 RH=INT(RND(1)*(50)+20)
11001 PRINT"{home}{down*20}{left}"RH
11002 RETURN
11100 POKE38219,5:POKE38220,5:POKE38221,5:POKE4427,81:POKE4428,81:POKE4429,81
11102 RETURN
12000 GOSUB10300:POKE36878,0:FORT=0TO5000:NEXT:CLR:GOTO800
13000 POKEDD,127:S3=-((PEEK(PB)AND128)=0):POKEDD,255
13005 P=PEEK(PA):S1=-((PAND8)=0):S2=((PAND16)=0):S0=((PAND4)=0)
13010 XX=S2+S3:YY=S0+S1:RETURN
14000 MI$=MID$(TI$,4,1):SE$=RIGHT$(TI$,2)
14001 POKE36878,0
14002 FORP=1TO7000:NEXT
14003 IFLE=6THENGOTO14024
14004 PRINT"{clear}you took ";MI$;" minutes ";SE$;" seconds"
14006 SC=INT((1300-(VAL(TI$)))/10)
14008 PRINT"{down}your score is " SC
14010 IF SC >100THENSC=100
14012 IFSC>=80THENGOTO14020
14014 IFLE<6ANDSC<80THENPRINT"{down}the pass mark is 80"
14016 IFLE<6ANDSC<80THENPRINT"{yellow}{down*2}it could be dangerous for you to attempt a"
14018 IFLE<6ANDSC<80THENPRINT"higher level":GOTO14024
14020 IF LE<6ANDSC>=80THENPRINT"{down}well done-you are safeto continue onto next"
14022 IF LE<6THENPRINT"level"
14024 IF LE=6THENPRINT"{clear}{down}{green}congratulations!! you have reached a very high standard of ";
14026 IFLE=6THENPRINT"flying skills and would do well to join a flying{space*3}club{red}"
14028 FOR P=1TO15000:NEXT
14030 CLR:GOTO 800
