5 REM
8 BS=1024:F=55296
33 POKE53280,0:POKE53281,0:PRINT"{142}{clear}{ct h}{down*4}{yellow}":CLR
34 C0=0:C1=1:C2=2:C3=3:C4=4:C5=5:C6=6:C7=7:C8=8:C9=9
35 SC=1024:LL=40
36 DIMA(19,11),B(19,11)
37 A$(1)="nord":A$(2)="ost":A$(3)="sued":A$(4)="west"
38 W$="{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}{cm e}{cm r}"
39 SP$="{space*47}"
40 M1=C0:PRINT"{reverse off}{green}{down*2}gib die dimension des labyrinths ein:"
41 INPUT"{down}wie breit";H
42 IFH=C0ORH>19THENPRINT"{up*2}";:GOTO41
43 INPUT"{down}wie lang";V
44 IFV=C0ORV>10THENPRINT"{up*2}";:GOTO43
45 PRINT"{down}nun wird ein"H"V"V"labyrinth erzeugt"
46 A=H*V+C1
47 Q=C0:Z=C0:X=INT(H*RND(1))+C1
48 AA=X
49 A(X,0)=C1:C=C2
50 R=X:S=C1:GOTO57
51 IFR<>HTHEN55
52 IFS<>VTHEN54
53 R=C1:S=C1:GOTO56
54 R=C1:S=S+C1:GOTO56
55 R=R+C1
56 IFA(R,S-C1)=C0THEN51
57 IFR-C1=C0THEN89
58 IFA(R-C1,S-C1)THEN89
59 IFS-C1=C0THEN73
60 IFA(R,S-C2)THEN73
61 IFR=HTHEN65
62 IFA(R+C1,S-C1)THEN65
63 X=INT(C3*RND(1))+C1
64 ONXGOTO120,124,128
65 IFS<>VTHEN68
66 IFZ=C1THEN71
67 Q=C1:GOTO69
68 IFA(R,S)THEN71
69 X=INT(C3*RND(1))+C1
70 ONXGOTO120,124,135
71 X=INT(C2*RND(1))+C1
72 GOTO70
73 IFR=HTHEN83
74 IFA(R+C1,S-C1)GOTO83
75 IFS<>VTHEN78
76 IFZ=C1THEN81
77 Q=C1:GOTO79
78 IFA(R,S)THEN81
79 X=INT(C3*RND(1))+C1
80 ONXGOTO120,128,135
81 X=INT(C2*RND(1))+C1
82 GOTO80
83 IFS<>VTHEN86
84 IFZ=C1THEN120
85 Q=C1:GOTO87
86 IFA(R,S)THEN120
87 X=INT(C2*RND(1))+C1
88 ONXGOTO120,135
89 IFS-C1=C0THEN107
90 IFA(R,S-C2)THEN107
91 IFR=HTHEN101
92 IFA(R+C1,S-C1)THEN101
93 IFS<>VTHEN96
94 IFZ=C1THEN99
95 Q=C1:GOTO97
96 IFA(R,S)THEN99
97 X=INT(C3*RND(1))+C1
98 ONXGOTO124,128,135
99 X=INT(C2*RND(1))+C1
100 GOTO98
101 IFS<>VTHEN104
102 IFZ=C1THEN124
103 Q=C1:GOTO105
104 IFA(R,S)THEN124
105 X=INT(C2*RND(1))+C1
106 ONXGOTO124,135
107 IFR=HTHEN115
108 IFA(R+C1,S-C1)THEN115
109 IFS<>VTHEN112
110 IFZ=C1THEN128
111 Q=C1:GOTO113
112 IFA(R,S)THEN128
113 X=INT(C2*RND(1))+C1
114 ONXGOTO128,135
115 IFS<>VTHEN118
116 IFZ=C1THEN51
117 Q=C1:GOTO119
118 IFA(R,S)THEN51
119 GOTO135
120 A(R-C1,S-C1)=C
121 C=C+C1:B(R-C1,S-C1)=C2:R=R-C1
122 IFC=ATHEN145
123 Q=C0:GOTO57
124 A(R,S-C2)=C
125 C=C+C1
126 B(R,S-C2)=C1:S=S-C1:IFC=ATHEN145
127 Q=C0:GOTO57
128 A(R+C1,S-C1)=C
129 C=C+C1:IFB(R,S-C1)=C0THEN131
130 B(R,S-C1)=C3:GOTO132
131 B(R,S-C1)=C2
132 R=R+C1
133 IFC=ATHEN145
134 GOTO89
135 IFQ=C1THEN141
136 A(R,S)=C:C=C+1:IFB(R,S-C1)=C0THEN138
137 B(R,S-C1)=C3:GOTO139
138 B(R,S-C1)=C1
139 S=S+C1:IFC=ATHEN145
140 GOTO57
141 Z=C1
142 IFB(R,S-C1)=C0THEN144
143 B(R,S-C1)=C3:Q=C0:GOTO51
144 B(R,S-C1)=C1:Q=C0:R=C1:S=C1:GOTO56
145 IFZ<>C1THENX=INT(H*RND(1))+C1:B(X,V-C1)=B(X,V-C1)+C1
146 GOSUB147:POKESC+12*LL+18-H+LL*V+2*AA,30:POKE55296+12*LL+18-H+LL*V+2*AA,7:GOTO173
147 GOSUB189:REM PRINT *** IRRGARTEN ***
148 M1=C1:PRINT"{clear}";:IFV=11THEN150
149 FORI=C1TO11-V:PRINT:NEXT
150 FORJ=VTOC1STEP-C1:PRINTSPC(19-H)
151 FORI=C1TOH:PRINT"{reverse on} ";
152 IFB(I,J-C1)=C0THEN156
153 IFB(I,J-C1)=C2THEN156
154 PRINT"{black} {green}";
155 GOTO157
156 PRINT" ";
157 NEXTI
158 PRINT" "
159 PRINTSPC(19-H)"{reverse on} ";
160 FORI=C1TOH:PRINT"{black} {green}";
161 IFB(I,J-C1)<C2THEN164
162 PRINT"{black} {green}";
163 GOTO165
164 PRINT" ";
165 NEXTI:PRINT
166 NEXTJ
167 PRINTSPC(19-H)"{reverse on}";:FORI=C1TOH
168 IFI=AATHEN170
169 PRINT"{space*2}";:GOTO171
170 PRINT" {black} {green}";
171 NEXTI
172 PRINT" ":RETURN
173 W=AA
174 FORJ=C0TOV-C1
175 FORI=C1TOH
176 IFJTHEN179
177 IFI=WTHENA(I,J)=(C3-B(I,J))*C2:GOTO181
178 M=C1:GOTO180
179 M=(A(I,J-C1)AND2)/C2
180 A(I,J)=(C3-B(I,J))*C2+M*C8
181 IFI=C1THENM=C1:GOTO183
182 M=(A(I-C1,J)AND4)/C4
183 A(I,J)=A(I,J)+M
184 NEXTI
185 NEXTJ
186 PRINT"{clear}";
187 X=W:Y=C0:Z=C1:EL=C1:ER=C1
188 GOTO259
189 PRINT:PRINT"{clear}{down*2}gesteuert wird mit dem joystick :"
190 PRINT"{down}{reverse on}v{reverse off}-bewegt dich einen schritt vorwaerts,"
191 PRINT"{down}{reverse on}l{reverse off}-drehung 90 grad nach links,"
192 PRINT"{down}{reverse on}r{reverse off}-drehung 90 grad nach rechts,"
193 PRINT"{down}{reverse on}feuer{reverse off}-hilfe!":POKE198,0:WAIT198,1
194 RETURN
195 GOSUB147:PRINT"{down}du bist hier {yellow}Q{green}, blickrichtung "A$(Z)
196 POKESC+LL*12+18-H+LL*V+2*X-(LL*2)*Y,81
197 POKE55296+LL*12+18-H+LL*V+2*X-(LL*2)*Y,7
198 IFY>VTHEN474
199 PRINT"{home}{down*24}was nun?";
200 JO=PEEK(56320)
201 IF(JOAND4)=0THENII=II+1:GOTO206
202 IF(JOAND8)=0THENII=II+1:GOTO208
203 IF(JOAND1)=0THENII=II+1:GOTO254
204 IF(JOAND16)=0THENII=II+5:GOTO195
205 GOTO200
206 Z=Z-C1:IFZ<C1THENZ=Z+C4
207 GOTO259
208 Z=Z+C1:IFZ>C4THENZ=Z-C4
209 GOTO259
210 RETURN
211 IFZ=C1THENA1=A-C1:B1=B-C1:GOTO216
212 IFZ=C2ANDB<VTHENA1=A:B1=B:GOTO216
213 IFZ=C3THENA1=A+C1:B1=B-C1:GOTO216
214 IFZ=C4ANDB>C1THENA1=A:B1=B-C2:GOTO216
215 EL=C1:RETURN
216 F=A(A1,B1):IFZ=C1THEN218
217 FORI=C2TOZ:F=(FAND14)/C2+(FANDC1)*C8:NEXT
218 EL=(FANDC2)/C2:RETURN
219 IFZ=C1THENA1=A+C1:B1=B-C1:GOTO224
220 IFZ=C2ANDB>C1THENA1=A:B1=B-C2:GOTO224
221 IFZ=C3THENA1=A-C1:B1=B-C1:GOTO224
222 IFZ=C4ANDB<VTHENA1=A:B1=B:GOTO224
223 ER=C1:RETURN
224 F=A(A1,B1):IFZ=C1THEN226
225 FORI=C2TOZ:F=(FAND14)/C2+(FANDC1)*C8:NEXT
226 ER=(FANDC2)/C2:RETURN
227 IFB=C0THEN235
228 IFB>VTHENE=C3:RETURN
229 F=A(A,B-C1):IFZ=C1THEN231
230 FORI=C2TOZ:F=(FAND14)/C2+(FANDC1)*C8:NEXT
231 C=FANDC1:D=(FAND4)/C4:E=(FAND2)/C2
232 IFC=C0THENGOSUB211
233 IFD=C0THENGOSUB219
234 RETURN
235 C=C0:D=C0:E=-C1
236 IFZ<>1THEN240
237 E=C1
238 IFA=WTHENE=C0
239 RETURN
240 IFZ=C3THENE=C2:RETURN
241 IFZ=C2ANDA=HTHENE=C2:RETURN
242 IFZ=C4ANDA=C1THENE=C2:RETURN
243 RETURN
244 IFE>C0THEN250
245 IFZ=C1THENB=B+C1:RETURN
246 IFZ=C2THENA=A+C1:RETURN
247 IFZ=C3THENB=B-C1:RETURN
248 IFZ=C4THENA=A-C1:RETURN
249 PRINT"{home}{down*10}{right*10}";:RETURN
250 IFE=C0THEN252
251 PRINT"{ct g}";
252 S=C1
253 RETURN
254 A=X:B=Y
255 GOSUB227
256 GOSUB244
257 X=A:Y=B
258 IFE>C0THEN198
259 A=X:B=Y
260 GOSUB227
261 REM *** ZEIGE 5 TIEFEN ***
262 FORT=C1TO5
263 GOSUB274
264 IFETHENT=C5:GOTO268
265 GOSUB244
266 GOSUB227
267 IFE>C1THENT=C5:GOTO268
268 NEXTT
269 IFX=WANDY=C0ANDZ=C1THEN273
270 IFX<>WANDY=C0ANDZ=C1THENGOSUB249:PRINT"{up*4}{right*3}aeussere wand":GOTO272
271 GOTO273
272 GOSUB249:PRINT"{up*2}{right*2}du bist aussen !!!"
273 GOTO198
274 ONTGOTO275,311,355,399,447
275 REM *** ZEIGE TIEFE 1 ***
276 PRINT"{clear}";
277 IFE>C2THENRETURN
278 IFE<C0ORE>C1THENGOSUB251:RETURN
279 FORI=C1TO21:IFC=C1THENPRINT"{right*8}Y";:GOTO285
280 IFY=C0THENPRINT"{right}{reverse on}{red}"MID$(W$,(IANDC1)+C1,C8)"{green}";:GOTO285
281 IFEL=C1THENPRINT"{right}{white}{cm +*8}{green}";:GOTO285
282 IFI=C2THENPRINT"{right}{cm @*7}{white}{cm +}{green}";:GOTO285
283 IFI<18THENPRINT"{right*8}{white}{cm +}{green}";:GOTO285
284 PRINT"{right}{reverse on}{space*7}{reverse off}{white}{cm +}{green}";
285 IFE=C0THENPRINTSPC(22);:GOTO288
286 IFY=C0THENPRINT"{reverse on}{red}"MID$(W$,(IANDC1)+C1,22)"{green}";:GOTO288
287 PRINT"{white}{cm +*22}{green}";
288 IFD=C1THENPRINT"T":GOTO294
289 IFY=C0THENPRINT"{reverse on}{red}"MID$(W$,(IANDC1)+C1,C8)"{green}":GOTO294
290 IFER=C1THENPRINT"{white}{cm +*8}{green}":GOTO294
291 IFI=C2THENPRINT"{white}{cm +}{green}{cm @*7}":GOTO294
292 IFI<18THENPRINT"{white}{cm +}{green}":GOTO294
293 PRINT"{white}{cm +}{reverse on}{green}{space*7}"
294 NEXT
295 FORI=C1TOC3:IFC=C1OR(X=WANDY=C0)THEN301
296 IFI<>C1THEN298
297 IFEL=C0THENPRINT"{right}{reverse on}{cm t*7}N";:GOTO302
298 PRINT"{right}{reverse on}"LEFT$(SP$,C8-I)"N"LEFT$(SP$,I-C1);
299 IFI=C1THEN302
300 GOTO303
301 PRINTSPC(C9-I)"{reverse on}{sh pound}"LEFT$(SP$,I-C1);:IFI<>C1THEN303
302 IFE=C0THENPRINT"{reverse on}{cm t*22}";:GOTO304
303 PRINT"{reverse on}{space*22}";
304 IFD=C1OR(X=WANDY=C0)THEN308
305 IFI<>C1THEN307
306 IFER=C0THENPRINT"{reverse on}M{cm t*7}":GOTO309
307 PRINT"{reverse on}"LEFT$(SP$,I-C1)"M"LEFT$(SP$,C8-I):GOTO309
308 PRINT"{reverse on}"LEFT$(SP$,I-C1)"{cm asterisk}"
309 NEXTI
310 RETURN
311 REM *** ZEIGE TIEFE 2 ***
312 PRINT"{home}";:Z$="{right*9}"
313 FORI=C1TOC2:PRINTZ$;:IFC=C0THEN316
314 PRINTSPC(I+C1)"M";SPC(C2-I);:GOTO319
315 PRINT"{right*3}M";:GOTO319
316 IFI=C1THENPRINTSPC(C4):GOTO319
317 IFEL=C1THENPRINT"{cm @*4}";:GOTO319
318 PRINT"{right*3}{cm @}";
319 IFE=C0ORI=C1THENPRINTSPC(14);:GOTO321
320 PRINT"{cm @*14}";
321 IFD=C0THEN323
322 PRINTSPC(C2-I)"N":GOTO326
323 IFI=C1THENPRINT:GOTO326
324 IFER=C1THENPRINT"{cm @*4}":GOTO326
325 PRINT"{cm @}"
326 NEXT
327 FORI=C1TO15:PRINTZ$;:IFC=C1THENPRINT"{right*3}Y";:GOTO332
328 IFEL=C1THENPRINT"{reverse on}{white}{cm +*4}{reverse off}{green}";:GOTO332
329 IFI=C3THENPRINT"{cm @*3}{reverse on}{white}{cm +}{reverse off}{green}";:GOTO332
330 IFI>12THENPRINT"{reverse on}{space*3}{white}{cm +}{reverse off}{green}";:GOTO332
331 PRINT"{space*3}{reverse on}{white}{cm +}{reverse off}{green}";
332 IFE=C0THENPRINT"{right*14}";:GOTO334
333 PRINT"{reverse on}{white}{cm +*14}{reverse off}{green}";
334 IFD=C1THENPRINT"T":GOTO339
335 IFER=C1THENPRINT"{reverse on}{white}{cm +*4}{green}":GOTO339
336 IFI=C3THENPRINT"{reverse on}{white}{cm +}{reverse off}{green}{cm @*3}":GOTO339
337 IFI>12THENPRINT"{reverse on}{white}{cm +}{green}{space*3}":GOTO339
338 PRINT"{reverse on}{white}{cm +}{green}"
339 NEXT
340 FORI=C1TO4:PRINTZ$;:IFC=C1THEN345
341 IFI<>C1THEN344
342 IFEL=C0THENPRINT"{reverse on}{cm t*3}N";:GOTO346
343 PRINT"{reverse on}{space*3}N";:GOTO346
344 PRINT"{reverse on}"LEFT$(SP$,C4-I)"N"LEFT$(SP$,I-C1);:GOTO347
345 PRINTSPC(C4-I)"{reverse on}{sh pound}"LEFT$(SP$,I-C1);:IFI<>C1THEN347
346 IFE=C0THENPRINT"{reverse on}{cm t*14}";:GOTO348
347 PRINT"{reverse on}{space*14}";
348 IFD=C1THEN352
349 IFI<>C1THEN351
350 IFER=C0THENPRINT"{reverse on}M{cm t*3}":GOTO353
351 PRINT"{reverse on}"LEFT$(SP$,I-C1)"M"LEFT$(SP$,C4-I):GOTO353
352 PRINT"{reverse on}"LEFT$(SP$,I-C1)"{cm asterisk}":GOTO353
353 NEXTI
354 RETURN
355 REM *** ZEIGE TIEFE 3 ***
356 PRINT"{home}{down*2}";:Z$=Z$+"{right*4}"
357 FORI=C1TO3:PRINTZ$;:IFC=C0THEN359
358 PRINTSPC(I-C1)"M"SPC(C3-I);:GOTO362
359 IFI<C3THENPRINT"{right*3}";:GOTO362
360 IFEL=C1THENPRINT"{cm @*3}";:GOTO362
361 PRINT"{right*2}{cm @}";
362 IFE=C0ORI<=C2THENPRINT"{right*8}";:GOTO364
363 PRINT"{cm @*8}";
364 IFD=C0THEN366
365 PRINTSPC(3-I)"N":GOTO369
366 IFI<C3THENPRINT:GOTO369
367 IFER=C1THENPRINT"{cm @*3}":GOTO369
368 PRINT"{cm @}"
369 NEXT
370 FORI=C1TO9:PRINTZ$;:IFC=C1THENPRINT"{right*2}Y";:GOTO375
371 IFEL=C1THENPRINT"{white}{cm +*3}{green}";:GOTO375
372 IFI=C2THENPRINT"{cm @*2}{white}{cm +}{green}";:GOTO375
373 IFI>C7THENPRINT"{reverse on}{space*2}{reverse off}{white}{cm +}{green}";:GOTO375
374 PRINT"{space*2}{white}{cm +}{green}";
375 IFE=C0THENPRINT"{right*8}";:GOTO377
376 PRINT"{white}{cm +*8}{green}";
377 IFD=C1THENPRINT"T":GOTO382
378 IFER=C1THENPRINT"{white}{cm +*3}{green}":GOTO382
379 IFI=C2THENPRINT"{white}{cm +}{green}{cm @*2}":GOTO382
380 IFI>C7THENPRINT"{white}{cm +}{green}{reverse on}{space*2}{reverse off}":GOTO382
381 PRINT"{white}{cm +}{green}"
382 NEXT
383 FORI=C1TO3:PRINTZ$;:IFC=C1THEN389
384 IFI<>C1THEN386
385 IFEL=C0THENPRINT"{reverse on}{cm t*2}N";:GOTO390
386 PRINT"{reverse on}"LEFT$(SP$,C3-I)"N"LEFT$(SP$,I-C1);
387 IFI=C1THEN390
388 GOTO391
389 PRINTSPC(C3-I)"{reverse on}{sh pound}"LEFT$(SP$,I-C1);:GOTO387
390 IFE=C0THENPRINT"{reverse on}{cm t*8}";:GOTO392
391 PRINT"{reverse on}{space*8}";
392 IFD=C1THEN396
393 IFI<>C1THEN395
394 IFER=C0THENPRINT"{reverse on}M{cm t*2}":GOTO397
395 PRINT"{reverse on}"LEFT$(SP$,I-C1)"{reverse on}M"LEFT$(SP$,C3-I):GOTO397
396 PRINT"{reverse on}"LEFT$(SP$,I-C1)"{cm asterisk}"
397 NEXTI
398 RETURN
399 REM **** ZEIGE TIEFE 4 ***
400 PRINT"{home}{down*5}";:Z$=Z$+"{right*3}"
401 FORI=C1TO2:PRINTZ$;:IFC=C0THEN403
402 PRINTSPC(I-C1)"M"SPC(C2-I);:GOTO406
403 IFI=C1THENPRINT"{right*2}";:GOTO406
404 IFEL=C1THENPRINT"{cm @*2}";:GOTO406
405 PRINT"{right}{cm @}";
406 IFE=C0ORI=C1THENPRINT"{right*4}";:GOTO408
407 PRINT"{cm @*4}";
408 IFD=C0THEN411
409 IFI=C1THENPRINT"{right}";
410 PRINT"N":GOTO414
411 IFI=C1THENPRINT:GOTO414
412 IFER=C1THENPRINT"{cm @*2}":GOTO414
413 PRINT"{cm @}"
414 NEXT
415 FORI=C1TO5:PRINTZ$;:IFC=C1THENPRINT"{right}Y";:GOTO420
416 IFEL=C1THENPRINT"{reverse on}{white}{cm +*2}{reverse off}{green}";:GOTO420
417 IFI=C1THENPRINT" {reverse on}{white}{cm +}{reverse off}{green}";:GOTO420
418 IFI>4THENPRINT"{reverse on}{cm t}{white}{cm +}{reverse off}{green}";:GOTO420
419 PRINT" {reverse on}{white}{cm +}{reverse off}{green}";
420 IFE=C0THENPRINT"{right*4}";:GOTO422
421 PRINT"{reverse on}{white}{cm +*4}{reverse off}{green}";
422 IFD=C1THENPRINT"T":GOTO427
423 IFER=C1THENPRINT"{reverse on}{white}{cm +*2}{green}":GOTO427
424 IFI=C1THENPRINT"{reverse on}{white}{cm +}{reverse off}{green}{cm @}":GOTO427
425 IFI>C4THENPRINT"{reverse on}{white}{cm +}{green} ":GOTO427
426 PRINT"{reverse on}{white}{cm +}{green}"
427 NEXT
428 FORI=C1TO2:PRINTZ$;:IFC=C1THEN434
429 IFI<>C1THEN431
430 IFEL=C0THENPRINT"{reverse on}{cm t}N";:GOTO437
431 PRINT"{reverse on}"LEFT$(SP$,C2-I)"N"LEFT$(SP$,I-C1);
432 IFI=C1THEN437
433 GOTO438
434 IFI=C1THENPRINT"{right}";
435 PRINT"{reverse on}{sh pound}";
436 IFI=C2THENPRINT" ";:GOTO438
437 IFE=C0THENPRINT"{reverse on}{cm t*4}";:GOTO439
438 PRINT"{reverse on}{space*4}";
439 IFD=C1THEN443
440 IFI<>C1THEN442
441 IFER=C0THENPRINT"{reverse on}M{cm t}":GOTO445
442 PRINT"{reverse on}"LEFT$(SP$,I-C1)"M"LEFT$(SP$,C2-I):GOTO445
443 PRINT"{reverse on}";:IFI=C2THENPRINT" ";
444 PRINT"{cm asterisk}"
445 NEXTI
446 RETURN
447 REM *** ZEIGE TIEFE 5 ***
448 PRINT"{home}{down*7}";:Z$=Z$+"{right*2}"
449 PRINTZ$;:IFC=C0THEN451
450 PRINT"M";:GOTO452
451 PRINT"{cm @}";
452 IFE=C0THENPRINT"{right*2}";:GOTO454
453 PRINT"{cm @*2}";
454 IFD=C0THEN456
455 PRINT"N":GOTO457
456 PRINT"{cm @}"
457 FORI=C1TO3:PRINTZ$;:IFC=C1THEN459
458 PRINT"{white}{cm +}{green}";:GOTO460
459 PRINT"Y";
460 IFE=C0THENPRINT"{right*2}";:GOTO462
461 PRINT"{white}{cm +*2}{green}";
462 IFD=C0THENPRINT"{white}{cm +}{green}":GOTO464
463 PRINT"T"
464 NEXT
465 PRINTZ$;:IFC=C1THEN467
466 PRINT"{reverse on}N";:GOTO468
467 PRINT"{reverse on}{sh pound}";
468 IFE=C1THENPRINT"{reverse on}{space*2}";:GOTO470
469 PRINT"{reverse on}{cm t*2}";
470 IFD=C1THEN472
471 PRINT"{reverse on}M":GOTO473
472 PRINT"{reverse on}{cm asterisk}"
473 RETURN
474 PRINT"{clear}{down*2}du bist aussen !"
475 IFM1THENPRINT"{down*4}":GOTO478
476 PRINT"{clear}hier ist der irrgarten":GOSUB211
477 GOSUB147
478 PRINT"du hast "II" schritte gebraucht":INPUT"{down*4}noch ein spiel";Z$
479 IFLEFT$(Z$,1)="j"THENCLR:GOTO33
