0 GOSUB10050
1 POKE55,255:POKE56,47:V=53248:POKEV+32,0:POKEV+33,0
5 FORI=12544TO12551:POKEI,0:NEXT:POKE54296,15
10 FORI=0TO111:READA:POKE12288+I,A:NEXT
12 POKEV+24,28
15 X=4:Y=17:SC=1024:CO=55296:KB=197:FU=250:VV=0
20 GOSUB2000
25 FORI=1TO25
30 P=INT(RND(1)*630):IFPEEK(SC+P)<>32THEN30
35 POKECO+P,10:POKESC+P,12:NEXTI
37 PRINT"{home}{red}cccccccccccccccccccccccc";
40 PRINT"{home}{down*15}{right}{reverse off}d{down}{left*2}efg";
45 PRINT"{home}{down}{left*3}{down*20}{left*3}{green}hij";
50 K=PEEK(KB):IFFU<2THEN60
52 IFK<>36THEN60
55 VV=VV-1:FU=FU-2:IFVV<-5THENVV=-5
56 POKE54273,72:POKE54272,169:POKE54277,15:POKE54276,0:POKE54276,129
60 VV=VV+.5:IFVV>10THENVV=10
65 D=VV/10:Y1=INT(Y+D):P=PEEK(SC+Y1*40+X):IFP=2ANDVV>0THENFU=FU-.05:VV=0:D=0
67 IFP=12THEN200
70 IFP=0THENX1=X-1:Y1=Y+1:FU=FU-.1:GOTO86
75 IFP=1THENX1=X+1:Y1=Y+1:FU=FU-.1:GOTO86
77 Y1=Y+D:IFY1<1THENY1=1:VV=0
78 IFFU<1THEN400
80 X1=X+(K=12)-(K=20):FU=FU+(K=12)+(K=20):IFX1<0THENX1=0
85 IFX1>39THENX1=0
86 IFX=X1ANDINT(Y1)=INT(Y)THEN90
87 IFPEEK(SC+INT(Y1)*40+X1)=12THEN200
88 IFPEEK(SC+INT(Y1)*40+X1)<>32THEN100
90 POKESC+INT(Y)*40+X,32:X=X1:Y=Y1
100 Y1=INT(Y):POKECO+Y1*40+X,7:POKESC+Y1*40+X,11
101 IFX>33ANDX<37ANDINT(Y)=19THEN300
102 POKESC+FU/10,32
105 GOTO50
200 POKESC+INT(Y)*40+X,32:Y=INT(Y1):POKECO+Y*40+X,2:POKESC+Y*40+X,13
205 POKE54273,34:POKE54272,75:POKE54277,127:POKE54276,0:POKE54276,129
210 FORI=1TO2000:NEXT:POKEV+24,20:PRINT"{clear}{reverse off}{yellow}you crashed into a mine{down*5}"
220 PRINT"{green}another game?":POKE54272,0:POKE54273,0
225 GETA$:IFA$="y"THEN12
230 IFA$<>"n"GOTO225
235 END
300 FORI=1TO2000:NEXT
305 POKEV+24,20:PRINT"{clear}{reverse off}{red}well done, you completed your mission{down*5}"
310 GOTO220
400 POKEV+24,20:FORI=1TO1000:NEXT:PRINT"{clear}you ran out of fuel!{down*6}":GOTO220
2000 PRINT"{clear}{white}{down*12}{right*11}@ba{right*35}@b";
2005 PRINT"ccc{right*34}@ccccca{right*21}";
2010 PRINT"{right*11}@ccccccca{right*24}bbb{right*3}@ccccccccc{right*5}";
2015 PRINT"{right*11}@a{right*6}ccca{right*2}cccccccccca{right*12}@bbcca{right*5}";
2020 PRINT"ccccbbccccccccccc{right*11}@ccccc{right*6}cccccccccccccccccbba{right*5}@";
2025 PRINT"bbcccccc{right*6}cccccccccccccccccccc{right*4}@cccccccccbbbbbaccccccccccc";
2030 PRINT"cccccccccca{right}@cccccccccccccccccccccccccccccccccccccc{right}cccccccccccc";
2035 PRINT"ccccc";
2040 PRINT"ccccccccccccccccccccccbccccccccccccccccc";
2045 PRINT"ccccccccccccccccccccccccccccccccccccccc";:POKE2023,3
2050 POKE56295,1:RETURN
10000 DATA1,3,7,15,15,31,95,255,128,192,224,240,248,248,252,255
10005 DATA149,255,255,255,255,255,255,255,255,255,255,255,255,255,255,255
10010 DATA0,0,24,126,126,60,24,36,0,0,1,2,2,2,5,5,66,129,0,0,0,0,0,0
10015 DATA0,0,128,64,64,64,160,160,0,0,0,0,63,68,131,4,0,0,0,0,255,0,255,24
10020 DATA0,0,0,0,252,34,193,32,0,120,68,254,255,66,231,66
10025 DATA0,0,0,16,40,16,0,0,8,56,252,63,60,52,34,64
10050 POKE53281,0:PRINT"{clear}to go left press z : to go right press c"
10060 PRINT"{space*11}to fire jets press m":FORT=1TO2000:NEXT:RETURN
