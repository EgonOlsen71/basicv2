10 S=30000:L=300:E=S+L:N$="TEST"
20 REM --PLACE RANDOM VALUES IN MEMORY--
30 FORA=STOE-1:POKEA,RND(1)*256:NEXT
40 REM --STORE THE FILENAME IN MEMORY--
50 FORA=1TOLEN(N$):POKEE+A-1,ASC(MID$(N$,A,1)):NEXT
60 REM --USE KERNAL TO SAVE THE DATA--
70 POKE132,EAND255
80 POKE133,E/256
90 POKE664,LEN(N$)
100 POKE128,SAND255
110 POKE129,S/256
120 POKE780,172
130 POKE781,EAND255
140 POKE782,E/256
150 SYS$FFD8
155 REMPRINT:PRINT"NORMAL LOAD":LOAD"TEST",1,1
156 REMPRINT:PRINT"NORMAL LOAD2":LOADN$,1,1
160 REM --LOAD THE DATA SOMEWHERE ELSE--
170 S2=32000
180 PRINT:PRINTN$;1;0;S2:LOADN$,1,0,S2
190 FORA=0TOL-1
200 IFPEEK(S+A)<>PEEK(S2+A)THENPRINT"LOAD FAILED!":END
210 NEXT:PRINT "OK(1)"
220 REM --LOAD THE DATA INTO BANK 5--
230 S2=40960
240 PRINT:PRINTN$;1;5;S2:LOADN$,1,5,S2
250 IFPEEK($9F61)<>5THENPRINT"RAM BANK NOT SET!":END
260 FORA=0TOL-1
270 IFPEEK(S+A)<>PEEK(S2+A)THENPRINT"BANK LOAD FAILED!":END
280 NEXT:PRINT "OK(2)"
290 REM --TEST WRAPPING INTO NEXT BANK--
300 S2=49152-100
310 PRINT:PRINTN$;1;2;S2:LOADN$,1,2,S2
320 IFPEEK($9F61)<>3THENPRINT"WRONG RAM BANK!":END
330 FORA=0TOL-1
340 M=S2+A:IFM>=49152THENM=M-8192
345 REM PRINT A;": ";PEEK(S+A),PEEK(M)
350 POKE$9F61,2-(M<S2)
360 IFPEEK(S+A)<>PEEK(M)THENPRINT"BANK WRAP FAILED!":END
370 NEXT:PRINT "OK(3)"
380 REM --TEST JUST BARELY WRAPPING--
390 PRINT:PRINTN$;1;2;49152-L:LOADN$,1,2,49152-L
400 IFPEEK($9F61)<>3THENPRINT"WRONG BANK ON BORDER CASE!":END
405 PRINT "OK(4)"
410 REM --TEST LOADING INTO VRAM--
420 S2=100
430 PRINT:PRINT"VLOAD";N$;1;1;S2:VLOADN$,1,1,S2
440 FORA=0TOL-1
450 IFPEEK(S+A)<>VPEEK(1,S2+A)THENPRINT"VLOAD FAILED!":END
460 NEXT:PRINT "OK(5)"
470 PRINT"SUCCESS!"