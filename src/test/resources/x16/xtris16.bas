0 REM XTRIS FOR THE COMMANDER X16, BY EGONOLSEN71 2019
5 REM  ******* SETUP ******* 
10 GOSUB 54000:XS=5:XD=10:XN=XS+INT(XD/2):GS=16
15 GT=GS:YA=0:PO=0:NS=GS:NP=0:NC=NS:D0=0:D1=0:D2=0:DF=0:OD=256
20 XP=10:YP=0:SC=0:WD=256:YH=25:EB=32:SB=207:SF=160:CB=99:GOSUB 50000
25 GOSUB 51000:GOSUB 63900:BS=BN:GOSUB 63900:GOSUB 56020:GOSUB 56500
30 RT=0:FT=0:CF=0:XF=0:LA$="":TT=TI:CH=SB:GOSUB 60000
35 REM  ******* GAME LOOP ******* 
40 TZ=TI: IF TZ-TT>=GS THEN YA=1:TT=TZ
45 GS=GT:GT=NS
60 FC=0:GETA$:IF A$="" AND YA=0 THEN 40
70 IF A$="" THEN 110
80 IF A$="S" AND LA$<>"S" AND GT>0 THEN FT=RT-1:FC=1:IF FT<0 THEN FT=3
90 IF A$="A" AND GT>0 THEN XF=-1
95 IF A$=" " THEN GT=0
100 IF A$="D" AND GT>0 THEN XF=1
102 IF A$="K" THEN 63700
110 LA$=A$:RM=0
115 IF FC=1 OR XF<>0 THEN GT=NS-3:GOSUB 62000
120 IF RM=1 THEN GOSUB 63950
125 XP=XP+XF:XF=0:RT=FT
130 IF YA>0 THEN GOSUB 62200:IF CF=1 THEN GOSUB 62100:GOTO 1000
135 GOSUB 63970
140 YA=0:GOTO 40
999 REM  ******* NEW BLOCK ******* 
1000 IF Y<=-1 THEN 40000
1010 XP=XN:BS=BN:GOSUB 63900:GOSUB 56500:GOTO 40
39999 REM  ******* GAME OVER MESSAGE ******* 
40000 PRINT "{HOME}{DOWN*20}"SPC(XS+XD+3)"{RED}GAME OVER"
40010 GET A$:IF A$<>"" THEN 40010
40020 GET A$:IF A$="" THEN 40020
40030 RUN
49999 REM  ******* SETUP BLOCKS ******* 
50000 DIM BM%(118)
50005 REM LONG ONE
50010 BM%(0)=0:BM%(1)=2:BM%(2)=4:BM%(3)=6
50020 BM%(4)=-2*OD+4:BM%(5)=-OD+4:BM%(6)=4:BM%(7)=OD+4
50030 BM%(8)=0:BM%(9)=2:BM%(10)=4:BM%(11)=6
50040 BM%(12)=-2*OD+4:BM%(13)=-OD+4:BM%(14)=4:BM%(15)=OD+4
50050 REM L-SHAPE LEFT
50060 BM%(16)=0:BM%(17)=-OD:BM%(18)=2:BM%(19)=4
50070 BM%(20)=0:BM%(21)=-OD:BM%(22)=2-OD:BM%(23)=OD
50080 BM%(24)=0:BM%(25)=2:BM%(26)=4:BM%(27)=4+OD
50090 BM%(28)=2:BM%(29)=2-OD:BM%(30)=2+OD:BM%(31)=OD
50092 REM L-SHAPE RIGHT
50093 BM%(32)=0:BM%(33)=2:BM%(34)=4:BM%(35)=4-OD
50094 BM%(36)=0:BM%(37)=-OD:BM%(38)=OD:BM%(39)=2+OD
50095 BM%(40)=0:BM%(41)=OD:BM%(42)=2:BM%(43)=4
50096 BM%(44)=2:BM%(45)=2-OD:BM%(46)=-OD:BM%(47)=2+OD
50100 REM CUBE
50110 BM%(48)=0:BM%(49)=2:BM%(50)=-OD:BM%(51)=2-OD
50120 BM%(52)=0:BM%(53)=2:BM%(54)=-OD:BM%(55)=2-OD
50130 BM%(56)=0:BM%(57)=2:BM%(58)=-OD:BM%(59)=2-OD
50140 BM%(60)=0:BM%(61)=2:BM%(62)=-OD:BM%(63)=2-OD
50150 REM STEP RIGHT
50160 BM%(64)=0:BM%(65)=2:BM%(66)=2-OD:BM%(67)=4-OD
50170 BM%(68)=0:BM%(69)=-OD:BM%(70)=2:BM%(71)=2+OD
50180 BM%(72)=0:BM%(73)=2:BM%(74)=2-OD:BM%(75)=4-OD
50190 BM%(76)=0:BM%(77)=-OD:BM%(78)=2:BM%(79)=2+OD
50200 REM STEP CENTER
50210 BM%(80)=0:BM%(81)=2:BM%(82)=2-OD:BM%(83)=4
50220 BM%(84)=0:BM%(85)=-OD:BM%(86)=2:BM%(87)=OD
50230 BM%(88)=2:BM%(89)=-OD:BM%(90)=2-OD:BM%(91)=4-OD
50240 BM%(92)=0:BM%(93)=2:BM%(94)=2-OD:BM%(95)=2+OD
50250 REM STEP LEFT
50260 BM%(96)=-OD:BM%(97)=2-OD:BM%(98)=2:BM%(99)=4
50270 BM%(100)=0:BM%(101)=2:BM%(102)=OD:BM%(103)=2-OD
50280 BM%(104)=-OD:BM%(105)=2-OD:BM%(106)=2:BM%(107)=4
50290 BM%(108)=0:BM%(109)=2:BM%(110)=OD:BM%(111)=2-OD
50300 BM%(112)=3:BM%(113)=6:BM%(114)=8:BM%(115)=7
50310 BM%(116)=5:BM%(117)=4:BM%(118)=2
50340 RETURN
51000 REM  ******* DRAW PLAYFIELD ******* 
51010 PRINT "{yellow}"chr$(147);:FOR I=0 TO 21
51020 PRINT SPC(XS);CHR$(125);SPC(XD);CHR$(125)
51030 NEXT:PRINT SPC(XS);CHR$(106);:FORI=1TOXD:PRINTCHR$(CB);:NEXT
51040 PRINT CHR$(107):DD=XS+XD+3
51050 PRINT CHR$(19)"{DOWN*10}"SPC(DD)CHR$(117);
51055 FOR I=0 TO 6:PRINTCHR$(CB);:NEXT:PRINT CHR$(105)
51056 PRINT SPC(DD)CHR$(125)" NEXT: "CHR$(125)
51060 FOR I=1 TO 5:PRINT SPC(DD)CHR$(125)SPC(7)CHR$(125):NEXT
51080 PRINT SPC(DD)CHR$(106);:FOR I=0 TO 6:PRINTCHR$(CB);:NEXT
51090 PRINT CHR$(107):RETURN
53999 REM  ******* SCREEN MODE INITIALIZATION ******* 
54000 SCREEN 0:POKE 646,7
54010 PRINTCHR$(147);"XTRIS..."
54030 RETURN
54999 REM  ******* DETECT COMPLETED LINES ******* 
55000 GOSUB 63500:AD=AD-SC:LY=9999:HY=-9999
55010 FOR I=OO TO OO+3:AA=BM%(I)+AD
55030 Y=INT(AA/WD):IF Y<LY THEN LY=Y
55040 IF Y>HY THEN HY=Y
55050 NEXT:YD=0
55060 FOR I=LY TO HY
55070 K=I*WD+(XS+1)*2+SC:BC=0:FOR XX=K TO K+(XD-1)*2 STEP 2
55075 IF XX<0 THEN 55090
55080 IF VPEEK(0,XX)=SF THEN BC=BC+1
55090 NEXT
55100 IF BC=XD THEN GOSUB 55500:YD=YD+1:I=I-1:HY=HY-1
55110 NEXT:GOSUB 56000:RETURN
55499 REM  ******* REMOVE COMPLETED LINES ******* 
55500 FOR II=I TO 1 STEP-1:K=II*WD+(XS+1)*2+SC
55510 F=0:FOR XX=K TO K+(XD-1)*2 STEP 2
55520 G=XX-WD:J=VPEEK(0,G):IF J=SF THEN F=F+1
55530 VPOKE 0,XX,VPEEK(0,G):VPOKE 0,XX+1,VPEEK(0,G+1):NEXT
55540 IF F=0 THEN RETURN
55550 NEXT:RETURN
55999 REM  ******* DRAW POINTS AND SPEED ******* 
56000 IF YD=0 THEN RETURN
56010 PO=PO+INT(5^YD)+(23-LY)*2:NP=NP+YD
56012 IF NP>=10 THEN NS=NS-2:NP=0:IF NS<4 THEN NS=4
56020 PRINT CHR$(19)"{DOWN}"SPC(XS+XD+4)"SCORE:         {LEFT*9}";PO
56030 PRINT SPC(XS+XD+4)"SPEED:    {LEFT*4}";INT((NC-NS)/2)
56040 RETURN
56499 REM  ******* DRAW NEXT BLOCK PREVIEW ******* 
56500 T1=YP:T2=XP:T4=CH
56510 YP=15:XP=XS+XD+6:CH=EB:GOSUB 60000
56520 T3=BS:BS=BN:CH=SB:GOSUB 60000
56530 BS=T3:YP=T1:XP=T2:CH=T4:RETURN
59999 REM  ******* RENDER BLOCK ******* 
60000 GOSUB 63500
60001 CV=BM%(112+BS)
60010 FOR I=OO TO OO+3:AA=BM%(I)+AD
60020 IF AA<0 THEN 60040
60030 VPOKE 0,AA+1,CV:VPOKE 0,AA,CH
60040 NEXT:RETURN
60999 REM  ******* DETECT COLLISION WHEN FALLING DOWN ******* 
61000 GOSUB 63500:AD=AD+WD*YA
61010 FOR I=OO TO OO+3:AA=BM%(I)+AD:IF AA<0 THEN 61060
61050 A2=VPEEK(0,AA):IF A2=SF OR A2=CB-32 THEN CF=1:RETURN
61060 NEXT:CF=0:RETURN
61999 REM  ******* DETECT COLLISION WHEN MOVING LEFT/RIGHT OR ROTATING *******
62000 GOSUB 63600
62010 FOR I=OO TO OO+3:AA=BM%(I)+AD:IF AA<0 THEN 62040
62030 A2=VPEEK(0,AA):IF A2<>EB AND A2<>SB THEN FT=RT:XF=0:RETURN
62040 NEXT:RM=1:RETURN
62099 REM  ******* COLLISION RESPONSE ******* 
62100 CF=0:YP=YP-YA:GOSUB 63000:GOSUB 55000:RETURN
62199 REM  ******* DROP BLOCK ******* 
62200 GOSUB 63950:GOSUB 61000:YP=YP+YA:RETURN
62999 REM  ******* RENDER SOLID BLOCK ******* 
63000 CH=SF:GOSUB 60000:DF=0:RETURN
63499 REM  ******* CALCULATE CURRENT ADDRESS OFFSET ******* 
63500 AD=YP*WD+XP*2+SC:OO=BS*16+RT*4:RETURN
63599 REM  ******* CALCULATE NEXT ADDRESS OFFSET ******* 
63600 AD=YP*WD+(XP+XF)*2+SC:OO=BS*16+FT*4:RETURN
63700 REM  ******* EXIT GAME ******* 
63710 SCREEN 2:PRINTCHR$(147);"GOODBYE!":END
63899 REM  ******* DETERMINE NEXT BLOCK ******* 
63900 BN=INT(RND(0)*7):YP=-1:RT=0:FT=0:XF=0:RETURN
63949 REM FLAG BLOCK FOR DELETION
63950 IF DF=0 THEN CH=EB:DF=1:D0=XP:D1=YP:D2=RT
63960 RETURN
63969 REM ******* DELETE REDRAW BLOCK ******* 
63970 IF DF=0 THEN RETURN
63980 AD=D1*WD+D0*2+SC:OO=BS*16+D2*4:GOSUB 60001
63990 CH=SB:GOSUB 60000:DF=0
63995 RETURN


