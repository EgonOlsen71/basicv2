0 REM *** SIMPLE RAYTRACER BY MARCO64      14.06.2008 ***
1 REM *** PORTED TO THE X16 BY EGONOLSEN71 30.09.2019 ***
5 GOSUB 20000:TI$="000000"
10 DIM KX(10), KY(10), KZ(10), KR(10), XD(4)
11 AK=3-1:XD(3)=3:XD(2)=12:XD(1)=48:XD(0)=192
12 KX(0)=3:   KY(0)=9: KZ(0)=50: KR(0)=9
13 KX(1)=-12: KY(1)=7: KZ(1)=80: KR(1)=7
14 KX(2)=-3:  KY(2)=3: KZ(2)=35: KR(2)=3
15 BX=0: BY=5: BZ=-17: BE=25
16 LX=-50: LY=300: LZ=50
17 GB=10:WI=160
21 FORY=0TO239:FORX=0TO319
25 GOSUB10000
30 REM
32 ZW=GB/320: RX=X*ZW-GB/2: RY=-Y*ZW+240/2*ZW: RZ=BE
33 ZW=SQR(RX*RX+RY*RY+RZ*RZ): RX=RX/ZW:RY=RY/ZW: RZ=RZ/ZW
34 OX=BX: OY=BY: OZ=BZ
40 :REM
41 GOSUB 90
42 ON -((OY+T*RY)>0) GOTO 50
43 :REM
44 LA=-OY/RY: SX=OX+LA*RX: SY=0: SZ=OZ+LA*RZ
45 ZW=(INT(SX/5) AND 1)+(INT(SZ/5) AND 1): FA=(ZW=1)+1
46 OX=SX: OY=SY: OZ=SZ: RX=OX+LX: RY=OY+LY: RZ=OZ+LZ: GOSUB90
47 ON -(K=-1) GOTO 80
48 FA=((X AND 1)<>(Y AND 1))+1
49 GOTO 80
50 :REM
51 IF K=-1 THEN FA=0: GOTO 80
60 :REM
62 X1=KX(K)*RX+KY(K)*RY+KZ(K)*RZ:X2=OX*RX+OY*RY+OZ*RZ:X3=RX*RX+RY*RY+RZ*RZ
63 LA=(X1-X2)/X3:F1=(OX+LA*RX)-KX(K):F2=(OY+LA*RY)-KY(K):F3=(OZ+LA*RZ)-KZ(K)
64 D=SQR(F1*F1 + F2*F2 + F3*F3)
65 IF (KR(K)*.95<D) THEN FA=1: GOTO80
70 :REM
71 OX=OX+T*RX: OY=OY+T*RY: OZ=OZ+T*RZ
72 NX=KX(K)-OX: NY=KY(K)-OY: NZ=KZ(K)-OZ
73 ZW=NX*RX+NY*RY+NZ*RZ: RX=RX-2*ZW*NX: RY=RY-2*ZW*NY: RZ=RZ-2*ZW*NZ
74 ZW=SQR(RX*RX+RY*RY+RZ*RZ): RX=RX/ZW: RY=RY/ZW: RZ=RZ/ZW
75 GOTO 40
80 :REM 
81 IF FA=0 THEN GOSUB 10100:GOTO 84
82 GOSUB 10000
84 NEXT:NEXT
85 PRINT"T: "+TI$
86 GETA$:IFA$=""THEN86
87 END
90 :REM
91 K=-1:T=99999
92 FOR I=0 TO AK
94 ZW= RX*RX + RY*RY + RZ*RZ:K1=KX(I):K2=KY(I):K3=KR(I):K4=KZ(I)
95 P=(2*(RX*(OX-K1)+RY*(OY-K2)+RZ*(OZ-K4)))/ZW
96 Q=((OX-K1)*(OX-K1)+(OY-K2)*(OY-K2)+(OZ-K4)*(OZ-K4)-K3*K3)/ZW
97 Z1=-P/2:Z2=((P*P)/4-Q)
99 IF Z2<0 THEN NEXT:RETURN
100 WU=SQR(Z2)
101 X1=Z1+WU: X2=Z1-WU
102 Z0=-((X1>.001)AND(X1<T)): Z1=-Z0+1: K=Z0*I+Z1*K: T=Z0*X1+Z1*T
103 Z0=-((X2>.001)AND(X2<T)): Z1=-Z0+1: K=Z0*I+Z1*K: T=Z0*X2+Z1*T
104 NEXT:RETURN
10000 REM
10010 XP=INT(X/4):CV=XD(X AND 3)
10020 PO=$4000+Y*WI+XP:VPOKE 0, PO, VPEEK(0,PO) OR CV
10030 RETURN
10100 REM
10110 XP=INT(X/4):CV=XD(X AND 3)
10120 PO=$4000+Y*WI+XP:VPOKE 0, PO, VPEEK(0,PO) AND (255-CV)
10130 RETURN
20000 REM
20010 VPOKE 15,2,64:VPOKE 15,1,64:POKE 218,40
20020 VPOKE 15,$3005,$10:VPOKE 15,$3001,$10:VPOKE 15,$3000,$A1
20030 A=66:B=0:GOSUB 20200
20040 A=24:B=1:GOSUB 20200
20050 A=20:B=2:GOSUB 20200
20060 A=17:B=3:GOSUB 20200
20070 POKE 646,3:PRINT CHR$(147);
20080 RETURN
20200 PV = VPEEK(15, $1000+A*2)
20210 VPOKE 15,$1000+B*2,PV
20220 PV = VPEEK(15, $1000+A*2+1)
20230 VPOKE 15,$1000+B*2+1,PV
20240 RETURN
