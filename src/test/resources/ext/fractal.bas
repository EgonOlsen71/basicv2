10 DIM R(255),G(255),B(255)
20 RP=20:GP=80:BP=100
30 FOR I=0TO255:R(I)=RP:G(I)=GP:B(I)=BP
40 RP=RP+7:GP=GP+9:BP=BP+5
50 IF RP>255 THEN RP=20
60 IF GP>255 THEN GP=20
70 IF BP>255 THEN BP=20
80 NEXT
100 XL = -1.2:XU = -1.185: REM XL = -2.000:XU = 0.500
110 YL = -0.195:YU = -0.185 :REM YL = -1.100:YU = 1.100
115 REPS = 255
116 ITERS% = 0
120 WIDTH = 800:HEIGHT = 600
130 XINC = (XU-XL)/WIDTH
140 YINC = (YU-YL)/HEIGHT
200 REM MAIN
207 GRON WIDTH,HEIGHT:CLEAR
210 FOR J = 0 TO HEIGHT - 1
220 FOR I = 0 TO WIDTH - 1
310 ISMND = -1
312 NREAL = XL + I * XINC
313 NIMG  = YL + J * YINC
315 RZ = 0: IZ = 0
316 R2Z = 0: I2Z = 0
320 FOR K = 0 TO REPS
330 R2Z = RZ*RZ - IZ*IZ
340 I2Z = 2*RZ*IZ
350 RZ = R2Z+NREAL
360 IZ = I2Z +NIMG
365 ITERS%=K
370 IF (RZ*RZ + IZ*IZ)>4 THEN ISMND=0:K=REPS
390 NEXT
620 IF ISMND<>0 THEN COLOR 0, 0, 0:GOTO 630
622 REM IF ISMND=0 THEN COLOR 32*(ITERS% AND 7), 32*(ITERS%/8 AND 7), 32*(ITERS%/16 AND 7)
625 IF ISMND=0 THEN COLOR R(ITERS%), G(ITERS%), B(ITERS%)
630 PLOT I,J
640 NEXT:NEXT
660 PRINT "DONE!"
690 REM GET A$:IF A$ = "" THEN 290
720 GSAVE"MANDELBROT.PNG"
740 GROFF
750 END